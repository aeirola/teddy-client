apply plugin: 'com.android.application'
apply plugin: 'play'
apply plugin: "androidsvgdrawable"


import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask
import org.ajoberstar.gradle.git.release.semver.NearestVersionLocator
import org.ajoberstar.grgit.Grgit

int createVersionCode() {
    def grgit = Grgit.open(project.file('..'))
    int versionCode = grgit.log().size()
    println "Version code: " + versionCode
    return versionCode
}

String createVersionName() {
    def grgit = Grgit.open(project.file('..'))

    def locator = new NearestVersionLocator()
    def version = locator.locate(grgit)
    String versionName = version.normal.toString()
    println "Version name: " + versionName
    return versionName
}

Properties localProperties = new Properties()
File localPropertiesFile = project.rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newDataInputStream())
}

android {
    signingConfigs {
        release {
            storeFile file("../" + (System.getenv("KEYSTORE_FILE") ?: localProperties.get("keystoreFile", "keys/signing_keystore.jks")))
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: localProperties.get("keystorePassword", "")
            keyAlias System.getenv("KEY_NAME") ?: localProperties.get("keyName", "")
            keyPassword System.getenv("KEY_PASSWORD") ?: localProperties.get("keyPassword", "")
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "fi.iki.aeirola.teddyclient"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode createVersionCode()
        versionName createVersionName()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

play {
    serviceAccountEmail = System.getenv("ACCOUNT_NAME") ?: localProperties.get("accountName", "")
    pk12File = file("../" + (System.getenv("ACCOUNT_KEY_FILE") ?: localProperties.get("accountKeyFile", "keys/google_play.p12")))
    track = 'alpha'
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.3'
    compile 'com.koushikdutta.async:androidasync:2.0.5'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
}

task generateResImages(type: SvgDrawableTask) {
    from = file('src/main/svg/res')
    to = file('src/main/res')
    createMissingDirectories = true
    overrideMode = 'ifModified'
    fallbackDensity = 'mdpi'
    targetedDensities = ['ldpi', 'mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    highResIcon = 'ic_launcher_teddy'

    doLast {
        // Cleanup old
        file('src/main/play/en-GB/listing/icon').deleteDir()

        // Move play icon to correct place
        file('src/main/play/en-GB/listing/icon').mkdirs()
        file('../ic_launcher_teddy.png').renameTo(file('src/main/play/en-GB/listing/icon/ic_launcher_teddy.png'))
    }
}

task generatePlayImages(type: SvgDrawableTask) {
    from = file('src/main/svg/play')
    to = file('src/main/play/en-GB/listing')
    createMissingDirectories = true
    overrideMode = 'ifModified'
    fallbackDensity = 'mdpi'
    targetedDensities = ['mdpi']
    outputFormat = 'PNG'

    doLast {
        // Cleanup old
        file('src/main/play/en-GB/listing/featureGraphic').deleteDir()

        // Move images to correct place
        file('src/main/play/en-GB/listing/featureGraphic').mkdirs()
        file('src/main/play/en-GB/listing/drawable/feature_graphic.png').renameTo(file('src/main/play/en-GB/listing/featureGraphic/feature_graphic.png'))
        file('src/main/play/en-GB/listing/drawable').deleteDir()
    }
}

task fetchScreenshots(description: 'Fetches test screenshots from emulator') {
    doLast {
        exec {
            commandLine "adb", 'pull', '/sdcard/test_screenshots', 'src/main/play/en-GB/listing/phoneScreenshots'
        }
    }
}

task publish(description: 'Builds, tests and publishes APK')
publish.dependsOn {
    [
            'build',
            'connectedCheck',
            'fetchScreenshots',
            'publishRelease',
    ]
}
